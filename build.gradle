plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'biz.aQute.bnd.builder'
}

import aQute.bnd.gradle.Bndrun
import aQute.bnd.gradle.Export
import aQute.bnd.gradle.Resolve

def VERSION = "1.0"

version = VERSION

//  bnd attempts to use ~/ for caching if this is unavailable the build will fail.
System.setProperty("bnd.home.dir", "$buildDir/bnd")

allprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    compileKotlin {
        kotlinOptions {
            apiVersion = "1.4"
            languageVersion = "1.4"
            jvmTarget = "11"
            suppressWarnings = true
        }
    }
}

subprojects {
    apply plugin: 'biz.aQute.bnd.builder'

    dependencies {
        compileOnly group: 'org.osgi', name: "osgi.core", version: osgiVersion
        compileOnly group: 'org.osgi', name: "osgi.annotation", version: osgiVersion
        compileOnly group: 'org.osgi', name: "osgi.cmpn", version: osgiVersion
        compileOnly group: "org.osgi", name: "org.osgi.service.component.annotations", version: osgiScrAnnotationVersion
        compileOnly group: "org.osgi", name: "org.osgi.service.cm", version: osgiCmVersion
        compileOnly group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
//        compileOnly group: "biz.aQute.bnd", name: "biz.aQute.bnd.annotation", version:  bndVersion
    }

    version = VERSION
}


configurations {
    runtimeClasspath {
        exclude group: "org.osgi", module: "osgi.core"
        exclude group: "org.osgi", module: "osgi.cmpn"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.apache.felix:org.apache.felix.framework:$felixVersion"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
    implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion

    implementation group: "org.apache.felix", name: "org.apache.felix.framework", version: felixVersion
    implementation group: "org.apache.felix", name: "org.apache.felix.scr", version: felixScrVersion
    runtimeOnly(group: 'org.apache.felix', name: 'org.apache.felix.framework.security', version: felixSecurityVersion) {
        exclude(group: 'org.apache.felix', module: "org.apache.felix.framework")
        exclude(group: 'org.osgi', module: "osgi.core")
    }

    implementation group: "org.osgi", name: "org.osgi.util.promise", version: osgiUtilPromiseVersion
    implementation group: "org.osgi", name: "org.osgi.util.function", version: osgiUtilFunctionVersion
    implementation group: "org.osgi", name: "org.osgi.service.serviceloader", version: osgiServiceServiceLoaderVersion

    implementation project(":bundles:A")
    implementation project(":bundles:ControlPlane")

    implementation "org.jetbrains.kotlin:kotlin-osgi-bundle:$kotlinVersion"
}

tasks.register("export", Export.class) {
    bndrun = 'export.bndrun'
    bundles = project.files(configurations.runtimeClasspath)
}

tasks.register('run', Bndrun.class) {
    bndrun = 'export.bndrun'
}
